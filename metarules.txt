letter = regex("[A-Za-z_]");
decimal = regex("[0-9]");
space = regex("(?: )*");

START = "start";
STRING = regex("\"(?:[^\\\"\n]|\\[abtnvfr\"])*\"");
VAR = letter {letter | decimal};
ALTER = space "|" space;
ASSIGN = space "=" space;
CONCAT = regex("(?: )+");
REGEX = "regex";
LPAREN = "(";
RPAREN = ")";
LSB = "[";
RSB = "]";
LCB = "{";
RCB = "}";
RCBPLUS = "}+";
END = ";";

regfunc = REGEX space LPAREN space STRING space RPAREN;
opt = LSB space rvalue space RSB;
rep1 = LCB space rvalue space RCBPLUS;
rep0 = LCB space rvalue space RCB;
grouping = LPAREN space rvalue space RPAREN;
endpoint = STRING | regfunc | opt | rep1 | rep0 | grouping;
alterop = mid space ALTER;
mid = endpoint | (alterop space endpoint);
rvalue = VAR | mid | (rvalue CONCAT mid);

stmt = (START | VAR) ASSIGN rvalue END;

program = {stmt}+;

start = program;